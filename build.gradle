plugins {
    id 'jacoco'
    id 'java-library'
}

sourceCompatibility = '11'

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'org.jeasy:easy-random-core:4.1.0'
    testImplementation 'io.github.glytching:junit-extensions:2.3.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.2'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': project.group
        )
    }
    from sourceSets.main.allJava
}

javadoc {
    options.charSet = 'UTF-8'
}

processResources {
    filesMatching('**logbook-lib.properties') {
        expand(
                'libName': project.name,
                'libVersion': project.version,
                'libVendor': project.group,
                'libRevision': System.env.GIT_COMMIT ?: 'no-commit'
        )
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                minimum = 0.80
            }
        }
    }
}

jacocoTestReport {
    reports {
        html.destination file('build/jacocoHtml')
    }
}
